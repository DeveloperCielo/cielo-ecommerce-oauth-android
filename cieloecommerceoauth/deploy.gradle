apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        aar(MavenPublication) {
            artifact("$buildDir/outputs/aar/cieloecommerceoauth-release.aar")
            groupId 'br.com.braspag'
            artifactId 'cieloecommerceoauth'
            version '1.0.0'

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified' && it.group != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    // Get Bintray credential from environment variable
    user = System.getenv('BINTRAY_USER') // Get bintray User
    key = System.getenv('BINTRAY_KEY') // Get bintray Secret Key
    configurations = ['archives']
    publications = ['aar']
    pkg {
        repo = 'cielo-ecommerce-oauth-android'
        name = 'cieloecommerceoauth'
        licenses = ['MIT']
        desc = ''
        vcsUrl = 'https://github.com/DeveloperCielo/cielo-ecommerce-oauth-android.git'
        publish = true
        version {
            name = '1.0.0'
            desc = "Example 1.0.0"
            released = new Date()
            vcsTag = '1.0.0'
        }
    }
}